plugins {
    id 'org.spongepowered.plugin' version '0.6'
    id "com.github.johnrengelman.shadow" version "1.2.3"
    id 'net.minecrell.licenser' version '0.2.1'
}

allprojects {
	group = 'fr.evercraft'
    version = '1.2-BETA'
}

subprojects {
    apply plugin: 'java'
	apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven'

    repositories {
		mavenCentral()
		maven {
			name = 'sponge'
			url = 'http://repo.spongepowered.org/maven'
		}
	}
	
	jar {
		manifest {
			attributes 'Implementation-Title': name,
					   'Implementation-Version': version
		}
	}
	
	dependencies {
		compile "org.spongepowered:spongeapi:5.0.0-SNAPSHOT"
	}
	
	libsDirName = new File(rootProject.projectDir, "plugins")
	
	license {
	    header = project.file('HEADER.txt')
	    include '**/*.java'
	    style {
	        java = 'JAVADOC' // Sets Java license header style to JAVADOC (/**)
	    }
	}
}

configure(subprojects.findAll {it.name != "EverAPI"}) {
	dependencies {
        compile project(":EverAPI")
    }
}

project(':EverMails') {
	apply plugin: 'com.github.johnrengelman.shadow'

	configurations {
    	shadow
	}

	dependencies {
		compile 'javax.mail:mail:1.4.7'
		shadow 'javax.mail:mail:1.4.7'
	}
	
	shadowJar {
		baseName = project.name
	   	classifier = null
	   	version = project.version
	
		configurations += [project.configurations.shadow]
	  	dependencies {
	    	include(dependency('javax.mail:mail:1.4.7'))
	  	}
	}
}