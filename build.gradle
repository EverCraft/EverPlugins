buildscript {
  repositories {
	jcenter()
	dependencies {
	  classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.+')
	}
  }
}

plugins {
	id "com.github.johnrengelman.shadow" version "1.2.3"
	id 'net.minecrell.licenser' version '0.3'
	//id 'org.spongepowered.event-impl-gen' version '5.0.2'
}

// Arguments :
ext.plugin_version = "0.1.5"
ext.spongeapi_version = "7.0.0"
ext.everplugins = project

version = "S" + ext.spongeapi_version + "-R"  + ext.plugin_version + "-BETA"
if (project.hasProperty("BUILD_NUMBER")) {
	version = version + '-' + project.property("BUILD_NUMBER")
}

ext.artifactory_user = "admin"
ext.artifactory_password = ""
if (project.hasProperty("USER")) {
	ext.artifactory_user = project.property("USER")
}
if (project.hasProperty("PASSWORD")) {
	ext.artifactory_password = project.property("PASSWORD")
}

allprojects {
	group = 'fr.evercraft'
	version = everplugins.version
	organization = 'EverCraft'
}

// Default tasks
defaultTasks 'clean', 'licenseFormat', 'build', 'shadowJar'


configure(subprojects.findAll {it.name != "SpongeAPI" && it.name != "EverPlugins"}) {
	apply plugin: 'groovy'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'maven'
	apply plugin: 'java'

	repositories {
		mavenCentral()
		maven {
			name = 'sponge'
			url = 'http://repo.spongepowered.org/maven'
		}
		maven {
			name = 'worldedit'
			url = 'http://maven.sk89q.com/artifactory/repo'
		}
	}
	
	dependencies {
		compile "org.spongepowered:spongeapi:" + spongeapi_version + "-SNAPSHOT"
		testCompile 'junit:junit:4.7'
		//compile project(':SpongeAPI')
	}
	
	jar {
		manifest {
			attributes 'Implementation-Title': name,
					   'Implementation-Version': version
		}
	}
	
	libsDirName = new File(rootProject.projectDir, "build/plugins")
	
	apply plugin: 'net.minecrell.licenser'
	
	processResources {
		from 'HEADER.txt'
	}
		
	license {
		header = everplugins.file('HEADER.txt')
		include '**/*.java'
		ignoreFailures = true
		newLine = false // Disables the new line
		ext {
			name = project.name
			organization = project.organization
			url = project.url
		}
	}
}

task('setupDecompWorkspace', dependsOn: 'genEventImpl')

configure(subprojects.findAll {it.name != "EverAPI" && it.name != "SpongeAPI"}) {
	dependencies {
		compile project(":EverAPI")
	}
}

project(':EverAPI') {
	apply plugin: 'com.jfrog.artifactory'
	
	dependencies {
		compile "com.sk89q.worldedit:worldedit-sponge:6.1.7-SNAPSHOT"
		compile "com.sk89q.worldedit:worldedit-core:6.1.4-SNAPSHOT"
		compile "io.netty:netty-all:4.0.23.Final"
	}
	
	// Replace
	def versionFile =  'src/main/java/fr/evercraft/everapi/EverAPI.java'
	def tempDir = 'build/tmp/sourcesCache'
	def versionFileName = 'EverAPI.java'
	compileJava.doFirst {
		copy {
			from(versionFile)
			into(tempDir)
		}
		ant.replace(file: versionFile, token: '{EVERPLUGINS_VERSION}', value: plugin_version)
		ant.replace(file: versionFile, token: '{SPONGEAPI_VERSION}', value: spongeapi_version + "-SNAPSHOT")
	}
	compileJava.doLast {
		copy {
			from(tempDir + '/' + versionFileName)
			into(project.file(versionFile).parent)
		}
	}
	
	// jFrog
	configurations {
		published
	}
	task sourceJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = 'sources'
	}
	artifactoryPublish {
		dependsOn sourceJar
	}
	artifacts {
		published sourceJar
	}
	artifactory {
		contextUrl = 'http://repo.evercraft.fr/artifactory'
		publish {
			repository {
				repoKey = 'Minecraft'
				username = "${artifactory_user}"
				password = "${artifactory_password}"
			}
		}
		publish {
			defaults {
				publishConfigs('archives', 'published')
				properties = ['qa.level': 'basic', 'dev.team' : 'core']
				properties {
					all '*:*:1.*:*@*', key1: 'val1', key2: 'val2'
					all 'org.jfrog.*:*:1.*:*@jar*', key3: 'val3', key4: 'val4'
				}
		  		publishPom = true
		  		publishIvy = false
			}
	  	}
	  	resolve {
			repository {
			  	repoKey = 'jcenter'
			  	maven = true	 
			}
	  	}
	}
	
	// Event
	/*apply plugin: 'org.spongepowered.event-impl-gen'
	genEventImpl {
		// The full qualified class name of the factory
		outputFactory = 'fr.evercraft.everapi.event.ESpongeEventFactory'
		// The path to your event interfaces
		include 'src/main/java/fr/evercraft/everapi/event/*'
	}*/
}

// Include JavaX
project(':EverMails') {
	apply plugin: 'com.github.johnrengelman.shadow'
	configurations {
		shadow
	}
	dependencies {
		compile 'javax.mail:mail:1.4.7'
		shadow 'javax.mail:mail:1.4.7'
	}
	shadowJar {
		baseName = project.name
	   	classifier = null
	   	version = project.version
	
		configurations += [project.configurations.shadow]
	  	dependencies {
			include(dependency('javax.mail:mail:1.4.7'))
	  	}
	}
}

task clean {
    delete "${buildDir}"
}

// Jenkins
task zip(type: Zip) {
	from "build/plugins"
	include '*.jar'
	
	archiveName 'All-EverPlugins-' + everplugins.version + ".zip"
	destinationDir file('build/plugins')
}